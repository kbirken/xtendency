grammar org.nanosite.xtendency.tracer.TracingExecutionContext with org.eclipse.xtext.xbase.Xbase

generate tracingExecutionContext "http://www.nanosite.org/xtendency/tracer/TracingExecutionContext"

import "http://www.eclipse.org/xtend"

ExecutionContext:
	(imports += Import)*
	'executionContext' name = ID '{'
		'project' projectName = STRING
		'class' clazz=[XtendTypeDeclaration | FQN] ':' function = [XtendFunction | FUNCTIONID]
		('initialize' '{' 
			
			('injector' '=' injector = XBlockExpression
			(injectedMembers += InjectedMember)*	
			)?
			
			(inits += InitBlock)*
		'}')?
		
		('tracingScope' scope = TracingScope)?
	'}'
;

TracingScope:
	'package' | 'project'
;

FUNCTIONID:
	ID '('(ID (',' ID)*)? ')'
;

InitBlock:
	param = STRING '=' expr = XBlockExpression
;

InjectedMember:
	'inject' name = ID 'with' type = JvmTypeReference
;

Import:
	'import' importedNamespace = QualifiedName
;

FQN:
	ID ('.' ID)*
;