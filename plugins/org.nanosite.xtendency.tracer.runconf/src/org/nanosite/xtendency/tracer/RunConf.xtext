grammar org.nanosite.xtendency.tracer.RunConf with org.eclipse.xtext.xbase.Xbase

generate runConf "http://www.nanosite.org/xtendency/tracer/RunConf"

import "http://www.eclipse.org/xtend"

RunConfiguration:
	(imports += Import)*
	'runConfiguration' name = ID '{'
		'project' projectName = STRING
		'class' clazz=[XtendTypeDeclaration | FQN] ':' function = [XtendFunction]
		('initialize' '{' 
			
			('injector' '=' injector = XBlockExpression
			(injectedMembers += InjectedMember)*	
			)?
			
			(inits += InitBlock)*
		'}')?
	'}'
;

InitBlock:
	param = STRING '=' expr = XBlockExpression
;

InjectedMember:
	'inject' name = ID 'with' type = JvmTypeReference
;

Import:
	'import' importedNamespace = QualifiedName
;

FQN:
	ID ('.' ID)*
;
	

